pkgname=st-git
_pkgname=st
pkgver=0.7.37.gb1338e9
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
# include config.h and any patches you want to have applied here
source=(
	'git://git.suckless.org/st#commit=b1338e9'
	'scrollback.diff'
	'colors.diff'
)
sha1sums=(
	'SKIP'
	'SKIP'
	'SKIP'
)

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	echo "prepare"
	local file
	cd "${_pkgname}"

	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			# add all patches present in source array
			patch -Np1 < "$srcdir/$(basename ${file})"
		fi
	done
	
	sed \
		-e '/char font/s/= .*/= "xos4 Terminus:size=12:antialias=false";/' \
		-e '/char worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/' \
		-e '/int defaultcs/s/= .*/= 7;/' \
		-e '/int borderpx/s/= .*/= 4;/' \
		-e '/int xfps/s/= .*/= 240;/' \
		-e '/int actionfps/s/= .*/= 120;/' \
		-i config.def.h
	#sed \
	#	-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
	#	-e 's/CFLAGS =/CFLAGS +=/g' \
	#	-e 's/LDFLAGS =/LDFLAGS +=/g' \
	#	-e 's/_BSD_SOURCE/_DEFAULT_SOURCE/' \
	#	-i config.mk
	sed \
		-e '/@tic/d' \
		-i Makefile

	# clean up old config file
	rm -f config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}


package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
}
